#ifndef TELEPORTS_MODULE
#define TELEPORTS_MODULE

#include "_utils.fos"
#include "teleports_h.fos"

import uint CountMapPlayers( Map@ map ) from "manager";

//~run teleports go 0 0 0
void go( Critter& cr, int p0, int p1, int p2 )
{
	ShowTeleports(cr);
}

void ShowTeleports( Critter& cr )
{
	InitTeleports();
	iMenuHandler@ handler = MenuTeleports( "", teleports, null );
	iDialogBox@ menu = OpenMenu( cr, "SelectTeleport", handler );
}

class MenuTeleports: CenteredMenuHandler
{
	bool isRoot;
	bool redrawing;
	string fullName;
	Teleport@ teleport;
	MenuTeleports@ parent;
	
	MenuTeleports( string fullName, Teleport@ teleport, MenuTeleports@ parent ) 
	{
		this.redrawing = true;
		this.isRoot = !valid(parent);
		this.fullName = fullName;
		@this.teleport = @teleport;
		@this.parent = @parent;
	}

	bool MenuUpdate( Critter& cr, iDialogBox& menu )
	{
		if( !redrawing )
		{
			return false;
		}
		
		uint l = teleport.list();
		for( uint n = 0; n < l; n++ )
		{
			if( menu.ButtonMsg( STR_LEX_NAME , "$name" + teleport.online(n) + " " + teleport.name(n) ) )
			{ 
				Teleport@ target = teleport.get(n);
				if( target.list() == 0 )
				{
					target.warp( cr );
					stopMenu();
					return false;
				}
				else
				{
					MenuTeleports@ new_menu = MenuTeleports( this.name(), target, this );
					new_menu.isRoot = false;
					return menu.OpenChild( this.name(), new_menu );
				}
			}
		}		
		
		if( !isRoot )
		{
			if( menu.ButtonMsg( STR_PREVIOUS ) )
			{
				return false;
			}
		}
		
		if( menu.ButtonMsg( STR_CLOSE_MENU_BUTTON ) )
		{
			stopMenu();			
			return false;
		}

		return true;
	}
	
	void stopMenu()
	{
		redrawing = false;
		
		MenuTeleports@ loopback = @parent;
		while( valid( loopback ) )
		{
			loopback.redrawing = false;
			@loopback = @loopback.parent;
		}
	}

	int getDescriptionFile()
	{
		return TEXTMSG_TEXT;
	}

	int getDescriptionLine()
	{
		return STR_TELEPORTS_MENU_MAIN;
	}
	
	string name() {
		return this.fullName + " " + this.teleport.name();
	}
	
	string@ Description( Critter& cr )
	{
		string info = "$name" + name();		
		
		return info;
	}
	
	bool ShouldRedraw( Critter& cr )
	{
		return true;
	}	
}

Teleport@ teleports;

void InitTeleports()
{
	if( valid( teleports ) )
	{
		return;
	}
	
	@teleports = Teleport( "Teleports" )
		.add( Teleport( "Убежище 15" )
			.add( Teleport( "Y = 1" )
				.add( "Заброшенный рудник",			424,	0, 253, 259 )
				.add( "Придорожный бар",			425,	0, 253, 259 )
				.add( "Подвал бара",				425,	1, 174, 196 )
				.add( "Пустошь",					426,	0, 253, 259 )
				.add( "Лагерь кочевников",			427,	0, 253, 259 )
				.add( "Пещера дикарей",				428,	0, 253, 259 )
			)
			.add( Teleport( "Y = 2" )
				.add( "Мусорный утес",				429,	0, 253, 259 )
				.add( "Руинный затор",				430,	0, 253, 259 )
				.add( "Сухой каньон",				431,	0, 253, 259 )
				.add( "Костлявый утес",				432,	0, 253, 259 )
				.add( "Призрачная радиовышка",		433,	0, 253, 259 )
			)
			.add( Teleport( "Y = 3" )
				.add( "Лесная община",				434,	0, 253, 259 )
				.add( "Деловой центр",				435,	0, 253, 259 )
				.add( "Центр этажи",				435,	1, 113, 93  )
				.add( "Подъем",						436,	0, 253, 259 )
				.add( "Лысеющая гора",				437,	0, 253, 259 )
				.add( "Подвал убежища",				437,	1,  88, 115 )
				.add( "Убежище 15",					437,	2, 214, 240 )
				.add( "Узкий каньон",				438,	0, 253, 259 )
			)
			.add( Teleport( "Y = 4" )
				.add( "Лесной поворот",				439,	0, 253, 259 )
				.add( "Дорожная яма",				440,	0, 253, 259 )
				.add( "Провал",						440,	1, 253, 259 )
				.add( "Начало ВПП",					441,	0, 253, 259 )
				.add( "Аэродром",					442,	0, 253, 259 )
				.add( "Восточный восход",			443,	0, 253, 259 )
			)
			.add( Teleport( "Y = 5" )
				.add( "Лесопилка",					444,	0, 253, 259 )
				.add( "Захолустье",					445,	0, 253, 259 )
				.add( "Ферма",						446,	0, 253, 259 )
				.add( "Мертвое озеро",				447,	0, 253, 259 )
				.add( "Шахта RobCo",				448,	0, 253, 259 )
			)
		)	
		.add( "Активация",					92,		0, 337, 184 )
		.add( "Пустота",					93,		0, 296, 178 )
		.add( "Рай",						95,		0, 129, 77  )
		.add( "Ад",							300,	0, 229, 177 )
		.add( "Арена 1",					96,		0, 440, 510 )
		.add( "Арена 2",					96,		1, 180, 270 )
		.add( "Тюрьма",						421,	0, 160, 131 )
	;
}

class Pos
{
	uint mapId;
	uint x;
	uint y;
	
	Pos( Map@ map, uint x, uint y )
	{
		this.mapId = valid( map ) ? map.Id : 0;
		this.x = x;
		this.y = y;
	}

	Pos( uint mapId, uint x, uint y )
	{
		this.mapId = mapId;
		this.x = x;
		this.y = y;
	}
	
	bool TransitToMap( Critter& cr )
	{
		if( mapId == 0 ) return false;
		
		Map@ map = GetMap( mapId );
		if( !valid( map ) ) return false;
		
		int attempt = 0;
		uint16 hexX = x, hexY = y;
		while( attempt < 10 )
		{
			if( !map.IsHexPassed( hexX, hexY ) )
				map.MoveHexByDir( hexX, hexY, Random( 0, 5 ), 1 );
			attempt++;
		}

		if( !map.IsHexPassed( hexX, hexY ) )
		{
			cr.Say( SAY_NETMSG, "|0xFFFF00 Hexes are busy." );
			return false;
		}
		
		cr.TransitToMap( mapId, hexX, hexY, cr.Dir );
		
		Location@ loc = map.GetLocation();
		if( valid(loc) )
			cr.SetWorldPos( loc.WorldX, loc.WorldY );
		else
			cr.Say( SAY_NETMSG, "|0xFFFF00 You have no idea where you are." );
		
		return true;
	}
	
	uint online() 
	{
		if( mapId == 0 )
		{
			return 0;
		}
		
		Map@ map = GetMap( mapId );
		if( !valid( map ) )
		{
			return 0;
		}

		return CountMapPlayers( map );
	}
}

class Teleport
{
	Teleport[] _list;
	uint list() 
	{ 
		return _list.length(); 
	}
	
	Teleport@ get( uint n ) 
	{ 
		if( n < 0 || n >= list() ) 
		{
			Log( "[" + name() + "] " + "Attempt of getting teleport #" + n + "/" + list() + "." );
			return null; 
		}
		
		return _list[n];
	}
	
	string _name;
	string name() { return _name; }
	string name( uint n ) 
	{ 
		Teleport@ target = get(n);
		return valid( target ) ? target.name() : "N-A";
	}

	Teleport( string name )
	{
		init( name );
	}
	
	Teleport( string name, uint locPID, uint mapN, uint x, uint y )
	{
		init( name );
		
		Map@ map = GetLocationMap( locPID, mapN );
		@this._pos = Pos( map, x, y );
		
		if( !valid( map ) )
		{
			this._name += " #" + locPID + "-" + mapN;
		}
	}
	
	void init( string name )
	{
		this._name = name;
		this._list.resize(0);
		@this._pos = null;
	}
	
	Teleport@ add( string name )
	{
		return add( Teleport( name ) );
	}
	
	Teleport@ add( string name, uint locPID, uint mapN, uint x, uint y )
	{
		return add( Teleport( name, locPID, mapN, x, y ) );
	}
	
	Teleport@ add( Teleport teleport )
	{
		_list.insertLast( teleport );
		return this;
	}

	Pos@ _pos;
	Teleport@ pos( Pos@ pos )
	{
		@this._pos = @pos;
		return this;
	}

	Teleport@ pos( Map@ map, uint x, uint y )
	{
		return pos( Pos( map, x, y ) );
	}

	Teleport@ pos( uint mapId, uint x, uint y )
	{
		return pos( Pos( mapId, x, y ) );
	}

	uint posOnline() 
	{
		return valid(_pos) ? _pos.online() : 0;
	}

	uint online() 
	{ 
		uint result = posOnline();
		for( uint n = 0, l = list(); n < l; n++ )
		{
			result += online(n);
		}
		
		return result;
	}
	
	uint online( uint n ) 
	{ 
		Teleport@ target = get(n);
		return valid( target ) ? target.online() : 0;
	}

	bool warp( Critter& cr )
	{
		return _pos.TransitToMap( cr );
	}
}

void ShowPlayers( Critter& cr )
{
	OpenMenu( cr, "SelectPlayer", MenuPlayers() );
	cr.RunClientScript( "client_gui@SetPlayerMenuActive", 1, 0, 0, null, null );
	cr.StatBase[ST_VAR0] = -1;
}

void unsafe_stopPlayerMenu( Critter& player, int param0, int param1, int param2, string@ param3, int[] @ param4 )
{
	if( !isGM(player) ) return;
	
	player.StatBase[ST_VAR0] = 0;
}

class MenuPlayers: CenteredMenuHandler
{
	bool MenuUpdate( Critter& cr, iDialogBox& menu )
	{
		if( cr.Stat[ST_VAR0] == 0 )
		{
			return false;
		}
		
		Critter@[] crs;
		uint count = GetAllPlayers( crs );
		for( uint n = 0; n < count; n++ )
		{
			string text;
			
			if( isGM( crs[n] ) )
			{//ГМ:
				text += "[GM]";
			}
			else if( crs[n].IsDead() )
			{//Труп:
				text += "x_X";
			}
			else if( crs[n].IsKnockout() )
			{
				if( crs[n].Stat[ ST_CURRENT_HP ] > 0 )
				{//Просто лежит
					text += "-_-";
				}
				else
				{//В минусах
					text += "o_O";
				}
			}
			else
			{//Цел, жив, орёл
				text += "^_^";
			}
			
			text += " " + GetPlayerName( crs[n].Id );
			
			if( menu.Button( text ) )
			{ 
				Map@ map = crs[n].GetMap();
				if( !valid( map ) )
				{
					cr.Say( SAY_NETMSG, "Цель на глобале, используйте '#cr2me " + crs[n].Id + "' что бы выдернуть её к себе." );
				}
				
				Location@ loc = map.GetLocation();
				cr.SetWorldPos(loc.WorldX, loc.WorldY);

				uint16 hexX, hexY;
				do
				{
					hexX = crs[n].HexX;
					hexY = crs[n].HexY;
					
					map.MoveHexByDir( hexX, hexY, Random( 0, 5 ), Random( 3, 5 ) );
				}
				while( !map.IsHexPassed( hexX, hexY ) );
				
				cr.TransitToMap( map.Id, hexX, hexY, 0 );
				return false;
			}
		}		
		
		return true;
	}

	int getDescriptionFile()
	{
		return TEXTMSG_TEXT;
	}

	int getDescriptionLine()
	{
		return STR_TELEPORTS_MENU_MAIN;
	}
	
	string@ Description( Critter& cr )
	{
		Map@ map = cr.GetMap();
		uint count = CountMapPlayers( map );
		
		Critter@[] crs;
		uint online = GetAllPlayers(crs);
		
		string info = "$name" + "\n" 
					+ "Онлайн " + online + " игроков." + "\n" 
					+ "На карте " + count + " игроков.";
		
		return info;
	}
	
	bool ShouldRedraw( Critter& cr )
	{
		return true;
	}	
}

#endif