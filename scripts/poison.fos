#ifndef POISON
#define POISON

#include "_utils.fos"
#include "poison_h.fos"

void AffectPoison( Critter& cr, int value )
{
    if( !IsHuman( cr ) )
	{
        if( cr.IsPlayer() )
		{
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_CRITTER_POISON_IMMUNE );
        }
		return;
    }
	
    if( cr.IsDead() )
	{
		cr.StatBase[ ST_POISONING_LEVEL ] = 0;
        return;
	}
	
    uint[] indexes;
    uint[] durations;
    bool isActive = cr.GetTimeEvents( CTE_POISON, indexes, durations, null ) != 0;
    int  levelVal = cr.Stat[ ST_POISONING_LEVEL ];
    int  resistVal = cr.Stat[ ST_POISON_RESISTANCE ];

    if( value > 0 )
	{
        value -= value * resistVal / 100;
    }
	else if( levelVal == 0 )
	{
        return;
	}
	
    cr.StatBase[ ST_POISONING_LEVEL ] = CLAMP( levelVal + value, 0, 1000 );
	
    levelVal = cr.Stat[ ST_POISONING_LEVEL ];

    uint strNum = 0;
	if( levelVal < 0 )
	{
		cr.StatBase[ ST_POISONING_LEVEL ] = 0;
        return;
	}
	else if( levelVal == 0 )
	{
        strNum = STR_POISON_FREE;
    }
	else if( value > 0 )
	{
        strNum = STR_POISON_GAIN;
    }
	else if( value < 0 )
	{
        strNum = STR_POISON_REDUCE;
    }
	
	if( strNum != 0 )
	{
        cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, strNum );
	}

    if( isActive ) // Try hurry event, if new stage aviable
	{
        if( levelVal == 0 )
		{
            cr.EraseTimeEvents( CTE_POISON );
        }
		else
		{
            uint oldDuration = durations[ 0 ];
			uint newDuration = GetPoisonDuration( cr );
            if( newDuration < oldDuration )
			{
                cr.ChangeTimeEvent( indexes[ 0 ], newDuration, 0 );
			}
		}
    }
	else
	{    // Try start new event
		cr.AddTimeEvent( "cte_Poison", GetPoisonDuration( cr ), CTE_POISON );
    }
}

void DropPoison( Critter& cr )
{
    cr.EraseTimeEvents( CTE_POISON );
    cr.StatBase[ ST_POISONING_LEVEL ] = 0;
}

void dropPoisonLevel( Critter& cr )
{
	int level = cr.StatBase[ ST_POISONING_LEVEL ];
	
	int value = 0;
	if( level < 50 )
	{
		value = 10;
	}
	else if( level < 300 )
	{
		value = 5;
	}
	else if( level < 600 )
	{
		value = 3;
	}
	else if( level < 800 )
	{
		value = 2;
	}
	else
	{
		value = 1;
	}
	
	//cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_POISON_PROCESS );
	cr.StatBase[ ST_POISONING_LEVEL ] -= value;
}

uint cte_Poison( Critter& cr, int identifier, uint& rate )
{
    if( cr.IsDead() )
	{
        return 0;
	}
	
    int levelVal = cr.Stat[ ST_POISONING_LEVEL ];
    if( levelVal > 0 )
	{
		dropPoisonLevel( cr );

		if( cr.IsKnockout() )
		{
			return GetPoisonDuration( cr ) * 2;
		}

        cr.StatBase[ ST_CURRENT_HP ]--;
		
        if( levelVal > 500 )
		{
			uint[] durations;
			uint[] indexes;
			uint amount = cr.GetTimeEvents( CTE_VOMIT, indexes, durations, null );
			if( amount < 1 )
			{
				cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_FEEL_SICK);
				uint vomitTime = Random( 5, 15 );
				cr.AddTimeEvent( "cte_vomit", REAL_MINUTE( vomitTime ), CTE_VOMIT, 0 );
				vomitTime --;
				cr.AddTimeEvent( "cte_vomit_warn", REAL_MINUTE( vomitTime ), CTE_VOMIT_WARN, 0 );
			}
		}
		
		if( cr.StatBase[ ST_CURRENT_HP ] <= getDeathHitPoints( cr ) )
		{
            cr.ToDead( Random( 0, 1 ) == 0 ? ANIM2_DEAD_FRONT : ANIM2_DEAD_BACK, null );
			cr.ParamBase[ CR_DEATH_STAGE ] = 100;
		}
	}

    if( cr.Stat[ ST_POISONING_LEVEL ] == 0 )
	{
        cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_POISON_FREE );
        return 0;
    }

    return GetPoisonDuration( cr );
}

uint GetPoisonDuration( Critter& cr )
{
	int level = cr.StatBase[ ST_POISONING_LEVEL ];
	
	if( level <= 0 )
	{
		return 0;
	}
	else if( level <= 50 )
	{
		return REAL_SECOND( 60 );
	}
	else if( level <= 300 )
	{
		return REAL_SECOND( 30 );
	}
	else if( level <= 600 )
	{
		return REAL_SECOND( 20 );
	}
	else if( level <= 800 )
	{
		return REAL_SECOND( 10 );
	}
	
	return REAL_SECOND( 5 );
}

void CheckPoisonEvent( Critter& cr )
{
	if( cr.GetTimeEvents( CTE_POISON, null, null, null ) == 0 && cr.Stat[ ST_POISONING_LEVEL ] > 0 )
	{
		cr.AddTimeEvent( "cte_Poison", GetPoisonDuration( cr ), CTE_POISON );
	}
}

uint cte_vomit( Critter& cr, int identifier, uint& rate )
{
	if( cr.IsDead() )
	{
		return 0; 
	}
	
	Map@ map = cr.GetMap();
	if( !valid( map ) )
	{
		return 0;	
	}
	
	_CritAnimatePickup( cr );
	cr.SayMsg( SAY_EMOTE, TEXTMSG_TEXT, STR_EMOTE_VOMITS );
	PlayGenericSound( map, cr.HexX, cr.HexY, "puke.ogg", 10 );
	uint16 hx = cr.HexX, hy = cr.HexY;
	map.MoveHexByDir( hx, hy, cr.Dir, 1 );
	map.AddItem( hx, hy, PID_SHIT_SMALL, 1 );
	cr.ParamBase[ ST_THIRST ] -= 200;
	cr.ParamBase[ ST_POISONING_LEVEL ] -= 100;
	cr.ParamBase[ ST_DRUNK ] -= 20;
	
	return 0; 
}

uint cte_vomit_warn( Critter& cr, int identifier, uint& rate )
{
	if( cr.IsDead() )
	{
		return 0; 
	}
	
	Map@ map = cr.GetMap();
	if( !valid( map ) )
	{
		return 0;	
	}
	
	cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_ABOUT_TO_VOMIT );
	
	return 0; 
}

void StartSepsis( Critter& cr, int chance )
{
	int roll = Random( 1, 100 );

	testInfo( cr, roll + " vs " + chance );
	
	if( roll < chance )
	{
		if ( cr.ParamBase[ CR_BLOOD_TOXIC ] == 0 ) 
		{	
			cr.ParamBase[ CR_BLOOD_TOXIC ] += 1;
			testInfo( cr, roll + " sepsis started " );
		}
		
		else if ( cr.ParamBase[ CR_BLOOD_TOXIC ] > 0 )
		{
			testInfo( cr, " already infected " );
		}
	}
}

#endif //POISON